{

EventPointerTable(0x7, PointerList)

ALIGN 4
PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN SelectionEvents MovingEvents MovedEvents Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1Hard
	POIN $0 $0 $0 // Skrimish Ally
	POIN $0 $0 $0 // Skrimish Enemy
	POIN BeginningScene EndingScene

TurnBasedEvents:
	TurnEvent(0, AlliesAppear, 1, PlayerPhase)
	TurnEvent(0, PiratesAppear, 4, PlayerPhase)
	END_MAIN

AlliesAppear:
	MUSC Comrades
	LoadUnits(ExtraUnits)
	FlashCursor(Naranja, 60)
	Text(Chapter_01_Allies_Appear)
	NoFade
	ENDA

PiratesAppear:
	LoadUnits(PirateReinforcements)
	NoFade
	ENDA

CharacterBasedEvents:

	END_MAIN

MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(EndingScene)
	END_MAIN

LocationBasedEvents:
	Village(0x8, TopLeftHouse, 1, 1)
	Village(0x9, BottomRightHouse, 14, 10)
	Seize(15,1)
	END_MAIN

TopLeftHouse:
	MUSI
	Text(Chapter_01_Top_Left_House, HouseBG)
	MUNO
	GiveItem(TealGem)
	NoFade
	ENDA

BottomRightHouse:
	MUSI
	Text(Chapter_01_Bottom_Right_House, HouseBG)
	MUNO
	GiveItem(Longbow)
	NoFade
	ENDA

SelectionEvents:

	END_MAIN

MovingEvents:

	END_MAIN

MovedEvents:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
Units1Hard:
	UNIT Olmos Lord 0 Level(1, Ally, 0) [2, 11] 0 0 0 0 [EspadaAncha, BronzeSword, Herb] DefaultAI
	UNIT Flor NomadTrooper_F 0 Level(1, Ally, 0) [0, 12] 0 0 0 0 [SilverSword, BronzeBow, BronzeSword] DefaultAI
	UNIT Guadalupe BowArmor 0 Level(9, Ally, 0) [0, 11] 0 0 0 0 [BronzeBow, Herb] DefaultAI
	UNIT Sosimo Fighter 0 Level(1, Ally, 0) [2, 12] 0 0 0 0 [BronzeAxe, Hatchet] DefaultAI
	UNIT Bastio Ohtli 0 Level(1, Ally, 0) [1, 12] 0 0 0 0 [BronzeAxe, Herb] DefaultAI
	UNIT Zapatera Soldier 0 Level(1, Ally, 0) [1, 11] 0 0 0 0 [BronzeLance, Herb] DefaultAI
	UNIT

ExtraUnits:
	UNIT Naranja Mercenary_F 0 Level(1, Ally, 0) [0, 13] 0 0 1 REDA2R13 [BronzeSword, Herb] DefaultAI
	UNIT Arbolito Hunter 0 Level(1, Ally, 0) [0, 13] 0 0 1 REDA1R13 [BronzeBow, Herb] DefaultAI
	UNIT

EnemyUnits:
	UNIT Hacha Pirate 0 Level(5, Enemy, 0) [15, 1] DropItem 0 0 0 [IronAxe, HandAxe] NeverMoveAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [15, 2] 0 0 0 0 [IronLance] AttackInDoubleRangeAI
	UNIT Goon_Ch01 Fighter 0 Level(1, Enemy, 1) [1, 8] 0 0 0 0 [BronzeAxe] AttackInRangeAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [5, 10] 0 0 0 0 [BronzeLance] DefaultAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [1, 3] 0 0 0 0 [BronzeLance] AttackInDoubleRangeAI
	UNIT Goon_Ch01 Mercenary 0 Level(1, Enemy, 1) [5, 6] DropItem 0 0 0 [BronzeSword, Herb] AttackInDoubleRangeAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [12, 11] 0 0 0 0 [BronzeLance] AttackInRangeAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [9, 6] 0 0 0 0 [BronzeLance] AttackInDoubleRangeAI
	UNIT Goon_Ch01 Pirate 0 Level(1, Enemy, 1) [12, 0] 0 0 0 0 [BronzeAxe] LootingAI
	UNIT Goon_Ch01 Pirate 0 Level(1, Enemy, 1) [16, 3] 0 0 0 0 [BronzeAxe] LootingAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [4, 2] 0 0 0 0 [BronzeLance] AttackInRangeAI
	UNIT Goon_Ch01 Hunter 0 Level(1, Enemy, 1) [15, 12] 0 0 0 0 [BronzeBow] AttackInRangeAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [13, 4] 0 0 0 0 [BronzeLance] AttackInRangeAI
	UNIT Goon_Ch01 Fighter 0 Level(1, Enemy, 1) [8, 1] 0 0 0 0 [BronzeAxe] AttackInRangeAI
	UNIT Goon_Ch01 Hunter 0 Level(1, Enemy, 1) [13, 1] 0 0 0 0 [BronzeBow] AttackInDoubleRangeAI
	UNIT Goon_Ch01 Deserter 0 Level(1, Enemy, 1) [8, 4] 0 0 0 0 [BronzeLance] AttackInDoubleRangeAI
	UNIT Goon_Ch01 Mercenary 0 Level(1, Enemy, 1) [3, 9] 0 0 0 0 [BronzeSword] DefaultAI
	UNIT Goon_Ch01 Fighter 0 Level(1, Enemy, 1) [9, 9] 0 0 0 0 [BronzeAxe] DefaultAI
	UNIT Goon_Ch01 Mercenary 0 Level(1, Enemy, 1) [13, 7] DropItem 0 0 0 [IronSword] AttackInDoubleRangeAI
	UNIT

PirateReinforcements:
	UNIT Goon_Ch01 Pirate 0 Level(1, Enemy, 1) [15, 1] 0 0 1 REDA14R2 [BronzeAxe] LootingAI
	UNIT Goon_Ch01 Pirate 0 Level(1, Enemy, 1) [15, 1] 0 0 1 REDA16R2 [BronzeAxe] LootingAI
	UNIT

Traps1:
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
	#ifdef _DEBUG_
		MNC2 0x4D // debug
		ENDA
	#endif
	LOAD1 0x1 Units1
	ENUN
	LOAD1 0x1 EnemyUnits
	ENUN
	
	MUSC Grim_Journey
	Background(Outside_Village_Day)
	Text(Chapter_01_Opening_1)
	MUSC Tension
	Text(Chapter_01_Opening_2)
	Background(Harbor)
	Text(Chapter_01_Opening_3)
	FADI 0x10
	CALL ClearBackground
	CAM1 Hacha
	FlashCursor(Hacha, 60)
	Text(Chapter_01_Opening_4)
	CAM1 Olmos
	FlashCursor(Olmos, 60)
	Text(Chapter_01_Opening_5)
	ENUT GuideUnlockFatigue
	NoFade
	ENDA

EndingScene:
	MUSC Into_the_Shadow_of_Victory
	Background(HouseBG)
	Text(Chapter_01_Ending_1)
	MUSC Treasured_Memories
	Text(Chapter_01_Ending_2)
	MUSC Grim_Journey
	Text(Chapter_01_Ending_3)
	MNC2 Chapter_02
	NoFade
	ENDA

}